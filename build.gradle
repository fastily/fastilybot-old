subprojects {
  apply plugin: 'eclipse'
  apply plugin: 'java'

  repositories {
    mavenLocal()
    jcenter()
  }
}

project(':fastilybot-shared') {
  apply plugin: 'java-library'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'

  version="0.0.1"
  group="fastilybot"

  dependencies {
    api "fastily:jwiki:1.6.1"
  }

  def pomConfig = {
    inceptionYear '2018'
    licenses {
      license {
        name 'GNU General Public License, Version 3.0'
        url 'https://www.gnu.org/licenses/gpl-3.0.txt'
        distribution 'repo'
      }
    }
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        pom.withXml {
          asNode().children().last() + pomConfig
        }

        artifact sourceJar
        artifact packageJavadoc
      }
    }
  }


  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
  }

  task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
  }


  javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    options {
      setMemberLevel JavadocMemberLevel.PUBLIC
      setAuthor true

      links "https://docs.oracle.com/javase/8/docs/api/"
      links "https://square.github.io/okio/1.x/okio/"
      links "https://square.github.io/okhttp/3.x/okhttp/"
      links "https://fastily.github.io/jwiki/docs/gson/"
      links "https://fastily.github.io/jwiki/docs/jwiki/"
    }
  }
}

project(':fastilybot-enwp') {
  apply plugin: 'application'

  mainClassName = "fastilybot.BMgr"

  dependencies {
    compile 'info.picocli:picocli:3.4.0'
    compile project(':fastilybot-shared')
  }


  task doDist(type: Jar) {
    baseName = "BMgr"

    from {
      configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }

    manifest {
      attributes 'Main-Class': 'fastilybot.BMgr'
    }

    exclude "META-INF/*.SF", "**/LICENSE*", "**/NOTICE*", "**/maven/**"

    with jar
  }
}

wrapper {
    gradleVersion = '4.9'
}